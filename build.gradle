
subprojects {
    /*
     * Not applying the bintray plugin. Couldn't get it working properly when declared
     * in subprojects, and when declared at root level of this script, it caused an httpcomponents
     * classpath issue with ml-gradle in the core project. So for now, each project has to
     * duplicate bintray publishing configuration.
     */

    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "checkstyle"
    apply plugin: "maven-publish"

    repositories {
        jcenter()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://developer.marklogic.com/maven2/" }
        mavenLocal()
    }

    dependencies {
        compile "org.springframework.batch:spring-batch-core:3.0.7.RELEASE"
        compile "com.marklogic:ml-javaclient-util:2.10.0"
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.1'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    sourceSets.test.resources.srcDir 'src/test/java'

    checkstyle {
        configFile = file("${project.rootDir}/dev-tools/checkstyle/checkstyle.xml")
        toolVersion = '7.1.2'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        baseName = artifactId
        version = project.version
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    jar {
        baseName = artifactId
        version = project.version
    }

    /*
     * Assumes each project has a single publication.
     */
    publishing {
        publications {
            mainJavaWithSources(MavenPublication) {
                groupId group
                artifactId project.property("artifactId")
                version project.version
                from components.java
                artifact sourcesJar
            }
        }
    }
}

project(':core') {
    dependencies {
        runtime "org.springframework.batch:spring-batch-core:3.0.7.RELEASE"

        //com.marklogic.spring.batch.Main extends com.marklogic.client.helper.LoggingObject
        runtime "com.marklogic:ml-javaclient-util:2.10.0-alpha"

        runtime "org.springframework:spring-jdbc:4.2.6.RELEASE"

        runtime "net.sf.jopt-simple:jopt-simple:5.0.1"
    }

    task testExamples(type: GradleBuild) {
        buildFile = '../examples/build.gradle'
        tasks = [ 'test' ]
    }

    test.dependsOn testExamples
}

project(':test') {
    dependencies {
        compile project(':core')
    }
}


